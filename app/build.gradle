import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

//APP名字
def appName = "MFH";
//是否是测试
def isTest = true;

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.mfh.builddemo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //APP配置签名一般不推荐把密码写到这里。
    signingConfigs {
        mySignings {
            storeFile file("../mfh.jks")
            keyAlias "Alias"
            keyPassword "123456"
            storePassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.mySignings
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.mySignings
        }
    }

    //修改apk名字并加上渠道名字
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileName
            if (isTest) {
                fileName = appName + "-v" + defaultConfig.versionName + variant.productFlavors[0].name + "测试版本" + getTime() + ".apk"
            } else {
                fileName = appName + "-v" + defaultConfig.versionName + variant.productFlavors[0].name + "正式版本" + getTime() + ".apk"
            }
            output.outputFile = new File(output.outputFile.parent, fileName)
            println("filePath：" + output.outputFile.getPath())
        }
    }

    //多渠道打包
    productFlavors {
        wandoujia {}
        c360 {}
        xiaomi {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile project(':umeng')
}

def getTime() {
    def format = new SimpleDateFormat("MM-dd HHmmss")
    format.setTimeZone(TimeZone.getDefault())
    return format.format(new Date())
}